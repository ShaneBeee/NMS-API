plugins {
    id 'java'
    id 'maven-publish'
    id("io.papermc.paperweight.userdev") version "1.6.2" // the latest version can be found on the Gradle Plugin Portal
}

// The Minecraft version we're currently building for
def minecraftVersion = '1.20.5'
// Where this builds on the server
def serverLocation = '1-20-5'
// Version of NMS-API
def projectVersion = '1.6.0'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    paperweight.paperDevBundle("${minecraftVersion}-R0.1-SNAPSHOT")
}

build {
    project.version = "${projectVersion}-${minecraftVersion}"
    dependsOn(reobfJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from jar // Mapped jar
    //from reobfJar // Obfuscated jar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/' + serverLocation + '/plugins/skript-reflect/'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.shanebeestudios'
            artifactId 'NMS-API'
            version projectVersion
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/javadoc")
    options {
        def javadocVer = Arrays.copyOfRange(minecraftVersion.split("\\."), 0, 2).join(".")
        links("https://jd.papermc.io/paper/" + javadocVer)
        title("NMS-API - ${projectVersion}")
    }
    exclude('com/shanebeestudios/nms/api/reflection')
}
