import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id("io.papermc.paperweight.userdev") version "1.7.1" // the latest version can be found on the Gradle Plugin Portal
}

// The Minecraft version we're currently building for
def minecraftVersion = '1.21'
// The Paper JavaDoc version to use
def javaDocVersion = '1.21'
// Where this builds on the server
def serverLocation = '1-21'
// Version of NMS-API
def projectVersion = '1.7.1'

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    // Skript
    maven { url 'https://repo.skriptlang.org/releases' }
}

dependencies {
    paperweight.paperDevBundle("${minecraftVersion}-R0.1-SNAPSHOT")

    // Skript
    compileOnly(group: 'com.github.SkriptLang', name: 'Skript', version: '2.8.7') {
        transitive = false
    }
}

build {
    project.version = "${projectVersion}-${minecraftVersion}"
    dependsOn(reobfJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

processResources {
    filter ReplaceTokens, tokens: ["version": projectVersion]
    filter ReplaceTokens, tokens: ["minecraft": minecraftVersion]
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from jar // Mapped jar
    //from reobfJar // Obfuscated jar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/' + serverLocation + '/plugins/'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.shanebeestudios'
            artifactId 'NMS-API'
            version projectVersion
        }
    }
}

javadoc {
    destinationDir = file("${buildDir}/javadoc")
    options {
        links("https://jd.papermc.io/paper/" + javaDocVersion)
        title("NMS-API - ${projectVersion}")
    }
    exclude('com/shanebeestudios/nms/api/reflection')
}
